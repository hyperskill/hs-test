plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    // id 'checkstyle' doesn't work with jitpack for some reason
    // id 'org.jetbrains.kotlin.jvm' version '1.3.60'
}

group 'org.hyperskill'
version '13.0.1'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    api 'junit:junit:4.13.2'

    // implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    api 'org.assertj:assertj-swing-junit:3.17.1'
    api 'org.apache.httpcomponents:httpclient:4.5.14'
    api 'com.google.code.gson:gson:2.10.1'
    api 'com.github.javaparser:javaparser-core:3.25.5'

    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'
}

compileJava.options.encoding = 'UTF-8'
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

// compileKotlin {
//     kotlinOptions.jvmTarget = "1.8"
// }

// compileTestKotlin {
//     kotlinOptions.jvmTarget = "1.8"
// }

wrapper {
    gradleVersion = '8.5'
}

task resolveDependencies {
    doLast {
        project.rootProject.allprojects.each { subProject ->
            subProject.buildscript.configurations.each { configuration ->
                if (configuration.isCanBeResolved()) {
                    configuration.resolve()
                }
            }
            subProject.configurations.each { configuration ->
                if (configuration.isCanBeResolved()) {
                    configuration.resolve()
                }
            }
        }
    }
}

task createFatJar(type: Jar) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes 'Main-Class': 'org.hyperskill.hstest.stage.StageTest'
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
}

publishing {
    publications {
        stable(MavenPublication) {
            groupId = "com.github.hyperskill"
            artifactId = "hs-test"
            version = project.version
            from components.java
        }

        snapshot(MavenPublication) {
            groupId = "com.github.hyperskill"
            artifactId = "hs-test"
            version = "release-SNAPSHOT"
            from components.java
        }
    }
    repositories {
        maven {
            url "https://packages.jetbrains.team/maven/p/hyperskill-hs-test/maven"
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
    }
}
